apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.android1ucsd.week1"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        // warns when code does not play nice with kotlin
        enable 'Interoperability'
    }
    compileOptions {
        // compiles the project with java 1.8, which is not the default
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            // required for robolectric
            includeAndroidResources = true
        }
    }
}

androidExtensions {
    experimental = true
    defaultCacheImplementation = "NONE" // also SPARSE_ARRAY, NONE, HASH_MAP
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Libraries that make backwards compatibility easier with older version of android
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    // Some kotlin extensions that make working with android classes easier in kotlin
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    // Special layout that allows for complex views
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // Efficient list view that
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha04'

    // ------------------- TESTING LIBRARIES ------------------- //

    // Core Testing Libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:rules:1.2.0-alpha03'
    testImplementation 'androidx.test:runner:1.2.0-alpha03'
    testImplementation 'androidx.test.ext:junit:1.1.0'

     // Testing helper libraries that help you interact with android view
     // (see: https://developer.android.com/training/testing/espresso/index.html)
     // these normally can only be used to run tests on the simulator and would use androidTestImplementation, but
     // because we add robolectric we can run them on our computer and use testImplementation.
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.2.0-alpha02'
    testImplementation 'androidx.test.espresso:espresso-intents:3.2.0-alpha02'

    // Allows us to run tests that depend on android classes on the computer instead of only on a simulator or device
    // (see: http://robolectric.org/androidx_test/)
    testImplementation 'org.robolectric:robolectric:4.2'


    // App Testing Libraries
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.2.0-alpha03'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0-alpha02'
}
